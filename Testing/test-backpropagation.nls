globals
[
  old-num-input-nodes
  old-uniform-hidden-layers?
  old-num-hidden-layers
  old-num-nodes-per-hidden-layer
  old-num-output-nodes
]

to test-backpropagation
  
  let num-examples-predefined 1000
  
  ifelse ( target-function = "n-bit-parity" )
  [
    set dataset generate-n-bit-parity-dataset num-examples-predefined
  ]
  [
    set dataset generate-test-dataset num-examples-predefined 
  ]
  
  backpropagation-train dataset num-hidden-layers
  
end


to-report target-answer
  
  let a [ activation ] of min-one-of input-nodes [ who ] = 1
  let b [ activation ] of max-one-of input-nodes [ who ] = 1
  
  report ifelse-value runresult ( word "my-" target-function " a b" ) [ 1 ][ 0 ] 
  
end

to-report my-nand [ a b ] 
  
  report not ( a and b )
  
end

to-report my-nor [ a b ]
  
  report not ( a or b )
  
end

to-report generate-test-dataset [ _count ]
  
  report n-values _count [ generate-dataset-pair ]

end

to-report my-and [ a b ]

  report a and b

end

to-report my-or [ a b ]
  
  report a or b
  
end

to-report my-xor [ a b ]
  
  report a xor b
  
end

;; a dataset pair is an input vector x with a target vector y
;; the input vector x consists of elements fed into the input neurons and thus has to have the
;; same dimension as the number of input neurons
;; the target vector y has a dimension equal to the number of output neurons
to-report generate-dataset-pair
  
  let a random 2
  let b random 2
  
  let c ifelse-value ( runresult ( word "( my-" target-function " ( a = 1 ) ( b = 1 ) )" ) ) [ 1 ] [ 0 ]
  
  let input-vector (list a b)
  let target-vector (list c)
 
  report ( list input-vector target-vector )
  
end

to user-entered-test [ in1 in2 ]
  
  let result get-result-for-user-test-input in1 in2
  let correct? ifelse-value ( result = target-answer ) [ "correct" ] [ "incorrect" ]
  
  user-message (word
    "The expected answer for " in1 " " target-function " " in2 " is " target-answer ".\n\n"
    "The network reported " result ", which is " correct? ".")
  
end

to-report get-result-for-user-test-input [ in1 in2 ]

  ask min-one-of input-nodes [who] [ set activation in1 ]
  ask max-one-of input-nodes [who] [ set activation in2 ]
  
  let af ifelse-value ( activation-function = "softsing" ) 
            [ task softsign-function ]
            [ ifelse-value ( activation-function = "hyperbolic-tangent" ) [ task hyperbolic-tangent-function ] [ task logistic-function ] ]
  
  propagate af num-hidden-layers
  
  let threshold 0.5
  
  report step ( [activation] of one-of output-nodes ) threshold

end